/*
URL: http://resources.codingthematrix.com/voting_record_dump109.txt
Downloading...
Name / Similarity between Chafee / Similarity between Santorum / Similarity between Democratic Party (v1) / Similarity between Democratic Party (v2)
Sununu / 12 / 33 / 10.3953 / 10.3953
McCain / 17 / 29 / 14.0233 / 14.0233
Burns / 21 / 41 / 23.0465 / 23.0465
DeWine / 31 / 39 / 26.3023 / 26.3023
Hagel / 25 / 41 / 20.6279 / 20.6279
Dole / 23 / 41 / 22.5349 / 22.5349
Wyden / 31 / 19 / 31.7907 / 31.7907
Biden / 31 / 19 / 34.8605 / 34.8605
Stevens / 28 / 40 / 23.5116 / 23.5116
Landrieu / 24 / 36 / 25.5116 / 25.5116
Bond / 25 / 45 / 21.0465 / 21.0465
Vitter / 19 / 39 / 21.093 / 21.093
Inhofe / 14 / 34 / 11.3023 / 11.3023
Bingaman / 36 / 20 / 30.9535 / 30.9535
Jeffords / 40 / 24 / 28.3256 / 28.3256
Thomas / 17 / 35 / 18.9302 / 18.9302
Burr / 25 / 31 / 21.8372 / 21.8372
McConnell / 27 / 43 / 22.5814 / 22.5814
Kohl / 31 / 21 / 31.9302 / 31.9302
Dayton / 23 / 19 / 33.186 / 33.186
Schumer / 26 / 20 / 31.3721 / 31.3721
Shelby / 22 / 40 / 23.3488 / 23.3488
Leahy / 28 / 13 / 32.4186 / 32.4186
Cochran / 27 / 43 / 22.9535 / 22.9535
Coleman / 30 / 38 / 26.6512 / 26.6512
Carper / 31 / 33 / 29.6977 / 29.6977
Feingold / 19 / 3 / 25.6047 / 25.6047
Murkowski / 29 / 41 / 24.4884 / 24.4884
Stabenow / 23 / 28 / 28.9302 / 28.9302
Dorgan / 25 / 21 / 31.9302 / 31.9302
Harkin / 26 / 14 / 32.2326 / 32.2326
Kennedy / 29 / 15 / 33.6744 / 33.6744
Cornyn / 21 / 41 / 17.5116 / 17.5116
Graham / 17 / 33 / 19.186 / 19.186
Bayh / 29 / 19 / 30.8372 / 30.8372
Obama / 31 / 19 / 31.6977 / 31.6977
Feinstein / 30 / 26 / 31.4186 / 31.4186
Kerry / 31 / 17 / 31.5581 / 31.5581
Hutchison / 25 / 41 / 21.1395 / 21.1395
Levin / 31 / 15 / 32.7209 / 32.7209
Lott / 26 / 40 / 21.3023 / 21.3023
Inouye / 28 / 16 / 29.0233 / 29.0233
Chambliss / 23 / 43 / 19.093 / 19.093
Roberts / 25 / 45 / 21.0465 / 21.0465
Smith / 29 / 41 / 24.4884 / 24.4884
Bunning / 21 / 41 / 17.186 / 17.186
Nelson2 / 27 / 43 / 22.9535 / 22.9535
Cantwell / 35 / 27 / 29.7907 / 29.7907
Dodd / 30 / 16 / 34.1395 / 34.1395
Hatch / 26 / 42 / 21.9767 / 21.9767
Domenici / 28 / 42 / 22.814 / 22.814
Durbin / 31 / 15 / 34.6744 / 34.6744
Boxer / 25 / 14 / 32.1395 / 32.1395
Johnson / 25 / 27 / 30.814 / 30.814
Ensign / 21 / 37 / 17.186 / 17.186
Enzi / 15 / 35 / 17.186 / 17.186
Lugar / 29 / 41 / 24.4884 / 24.4884
Craig / 23 / 35 / 23.2326 / 23.2326
Akaka / 30 / 13 / 32.4651 / 32.4651
Voinovich / 31 / 39 / 23.6512 / 23.6512
Martinez / 23 / 39 / 19.2558 / 19.2558
Rockefeller / 23 / 19 / 27.8372 / 27.8372
Snowe / 28 / 28 / 30.9302 / 30.9302
Clinton / 29 / 27 / 31.3256 / 31.3256
DeMint / 18 / 38 / 14.6744 / 14.6744
Santorum / 24 / 45 / 21.2093 / 21.2093
Collins / 29 / 27 / 32.3488 / 32.3488
Thune / 20 / 40 / 22.0698 / 22.0698
Frist / 27 / 39 / 22.5349 / 22.5349
Baucus / 29 / 27 / 30.4186 / 30.4186
Chafee / 43 / 24 / 28.8837 / 28.8837
Sarbanes / 31 / 15 / 34.6744 / 34.6744
Grassley / 25 / 45 / 21.0465 / 21.0465
Allen / 25 / 45 / 21.0465 / 21.0465
Lieberman / 31 / 18 / 25.0233 / 25.0233
Coburn / 12 / 26 / 10.9535 / 10.9535
Sessions / 22 / 42 / 18.4186 / 18.4186
Bennett / 28 / 40 / 23.5116 / 23.5116
Warner / 29 / 41 / 24.4884 / 24.4884
Pryor / 31 / 37 / 28.5349 / 28.5349
Reed / 31 / 13 / 33 / 33
Brownback / 25 / 41 / 20.7674 / 20.7674
Lautenberg / 27 / 15 / 32.3488 / 32.3488
Reid / 33 / 21 / 33.6047 / 33.6047
Talent / 25 / 45 / 21.0465 / 21.0465
Isakson / 23 / 43 / 19.093 / 19.093
Alexander / 24 / 40 / 20 / 20
Crapo / 18 / 38 / 20.093 / 20.093
Lincoln / 35 / 29 / 31.3256 / 31.3256
Gregg / 21 / 32 / 17.814 / 17.814
Murray / 37 / 22 / 31.5814 / 31.5814
Conrad / 29 / 19 / 31.0465 / 31.0465
Specter / 26 / 34 / 26.2093 / 26.2093
Allard / 23 / 43 / 19.093 / 19.093
Salazar / 30 / 32 / 28.9767 / 28.9767
Kyl / 21 / 41 / 17.8837 / 17.8837
Byrd / 24 / 20 / 29.1628 / 29.1628
Nelson1 / 29 / 35 / 27.186 / 27.186
Mikulski / 27 / 15 / 33.2326 / 33.2326

Jeffords is most similar to Chafee
Feingold is least similar to Santorum
Democratic Party (v1) ≒ Biden
Democratic Party (v2) ≒ Biden
Inhofe and Feingold are bitter rivals (similarity = -3 )
*/

void main()
{
	import std.algorithm : filter, map;
	import std.stdio : writeln, write, readln;
	import std.conv : to;
	import std.range : array;
	import std.string : chomp, split;
	import std.net.curl : get, CurlException;

	string url = "http://resources.codingthematrix.com/voting_record_dump109.txt";
	"URL: ".writeln(url);
	"Downloading...".writeln;
	string txt = get(url).to!string;

	auto strlist = txt.chomp.split('\n').array;
	auto democratic_party = strlist.map!split
		.filter!(s => s[1] == "D")
		.map!(s => s[0])
		.array;
	auto vd = strlist.createVotingDict();
	auto average = vd.findAverageRecord(democratic_party);

	"Name / Similarity between Chafee / Similarity between Santorum / Similarity between Democratic Party (v1) / Similarity between Democratic Party (v2)"
		.writeln;

	foreach (k, v; vd)
		writeln(k, " / ", policyCompare(vd, "Chafee", k), " / ", policyCompare(vd,
				"Santorum", k), " / ", findAverageSimilarity(vd, democratic_party,
				k), " / ", findSimilarity(average, v.to!(double[])));

	writeln;
	writeln(vd.mostSimilar("Chafee"), " is most similar to Chafee");
	writeln(vd.leastSimilar("Santorum"), " is least similar to Santorum");
	writeln("Democratic Party (v1) ≒ ", vd.mostSimilarBetwweenPartyV1(democratic_party));
	writeln("Democratic Party (v2) ≒ ", vd.mostSimilarBetwweenPartyV2(democratic_party));
	auto rivals = bitterRivals(vd);
	writeln(rivals[0], " and ", rivals[1], " are bitter rivals (similarity = ",
			vd.policyCompare(rivals[0], rivals[1]), " )");
}

long[][string] createVotingDict(string[] strlist)
{
	import std.algorithm : map;
	import std.range : array;
	import std.string : split;
	import std.conv : to;

	long[][string] result;
	foreach (str; strlist)
	{
		auto s = str.split().array;
		long[] record = s[3 .. $].to!(long[]).array;
		result[s[0]] = record;
	}
	return result;
}

T findSimilarity(T)(T[] a, T[] b)
{
	import std.algorithm : map, sum;
	import std.range : zip;

	return zip(a, b).map!(x => x[0] * x[1]).sum();
}

long policyCompare(long[][string] vd, string senator_a, string senator_b)
{
	return findSimilarity(vd[senator_a], vd[senator_b]);
}

string mostSimilar(long[][string] vd, string senator)
{
	string result;
	long similarity = long.min;
	foreach (key, value; vd)
	{
		if (key != senator)
		{
			long _similarity = vd.policyCompare(senator, key);
			if (similarity < _similarity)
			{
				similarity = _similarity;
				result = key;
			}
		}
	}
	return result;
}

string leastSimilar(long[][string] vd, string senator)
{
	string result;
	long similarity = long.max;
	foreach (key, value; vd)
	{
		if (key != senator)
		{
			long _similarity = vd.policyCompare(senator, key);
			if (_similarity < similarity)
			{
				similarity = _similarity;
				result = key;
			}
		}
	}
	return result;
}

double findAverageSimilarity(long[][string] vd, string[] senator_set, string senator)
{
	import std.algorithm : map, sum;
	import std.conv : to;

	long s = senator_set.map!(s => vd.policyCompare(s, senator)).sum();
	return s.to!double / senator_set.length;
}

string mostSimilarBetwweenPartyV1(long[][string] vd, string[] party)
{
	string result;
	double similarity = -double.infinity;
	foreach (key, value; vd)
	{
		double _similarity = vd.findAverageSimilarity(party, key);
		if (similarity < _similarity)
		{
			similarity = _similarity;
			result = key;
		}
	}
	return result;
}

double[] findAverageRecord(long[][string] vd, string[] party)
{
	import std.algorithm : map, reduce;
	import std.conv : to;
	import std.range : zip, array;

	return party.map!(senator => vd[senator].to!(double[]))
		.reduce!((a, b) => zip(a, b).map!(x => x[0] + x[1]).array)
		.map!(x => x / party.length)
		.array;
}

string mostSimilarBetwweenPartyV2(long[][string] vd, string[] party)
{
	import std.conv : to;

	double[] average = vd.findAverageRecord(party);
	string result;
	double similarity = -double.infinity;
	foreach (key, value; vd)
	{
		double _similarity = findSimilarity(value.to!(double[]), average);
		if (similarity < _similarity)
		{
			similarity = _similarity;
			result = key;
		}
	}
	return result;
}

string[2] bitterRivals(long[][string] vd)
{
	string[2] result;
	long similarity = long.max;
	foreach (keyA, valueA; vd)
		foreach (keyB, valueB; vd)
		{
			if (keyA != keyB)
			{
				long _similarity = findSimilarity(valueA, valueB);
				if (_similarity < similarity)
				{
					similarity = _similarity;
					result = [keyA, keyB];
				}
			}
		}
	return result;
}
